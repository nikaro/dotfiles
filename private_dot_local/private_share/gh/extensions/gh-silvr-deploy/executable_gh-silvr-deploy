#!/usr/bin/env fish

# `--demo` to target demo environment
# `--prod` to target production environment
# `--web` to open workflow run in browser
# `--push` to git push local branch
argparse --ignore-unknown "prod" "demo" "web" "push" -- $argv

# get target environment
if test -n "$_flag_prod"
    set environment "prod"
else if test -n "$_flag_demo"
    set environment "demo"
else
    set environment "staging"
end

# get current branch name
set branch (git rev-parse --abbrev-ref HEAD)

# git push
if test "$branch" != "master" && test -n "$_flag_push"
    git push
end

# start deployment
echo '{"confirm": "true", "environment": "'$environment'", "debug_ssh": "false"}' | \
    jq | \
    gh workflow run deploy.yml --ref=$branch --json

# open in browser
if test -n "$_flag_web"
    sleep 3
    open (
        gh run list \
            --workflow=deploy.yml \
            --branch=debug-client-ip \
            --limit=1 \
            --json=url \
            --jq='.[].url'
    )
end
